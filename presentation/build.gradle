plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.1'
}

def config = rootProject.ext

android {
    compileSdkVersion config.targetSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        testInstrumentationRunner config.testInstrumentationRunner
        consumerProguardFiles config.consumerProguardFiles
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

apply from: '../dependencies-all.gradle'

dependencies {
    implementation project(':data')
    implementation project(':domain')

    commonLang()
    core()
    coroutines()
    hilt()
    lifecycle()
    paging()
    testing()

    // Core - https://developer.android.com/jetpack/androidx/releases/core
    implementation 'androidx.core:core-ktx:1.3.2'
    // Appcompat - https://developer.android.com/jetpack/androidx/releases/appcompat
    implementation 'androidx.appcompat:appcompat:1.2.0'
    // Constraintlayout - https://developer.android.com/jetpack/androidx/releases/constraintlayout
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    // Recyclerview - https://developer.android.com/jetpack/androidx/releases/recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Navigation component - https://developer.android.com/guide/navigation/navigation-getting-started
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Material Components - https://material.io/develop/android/docs/getting-started/
    implementation 'com.google.android.material:material:1.3.0-rc01'

    // Glide - https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // MPAndroidChart - https://github.com/PhilJay/MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
}

ktlint {
    android = true
}
